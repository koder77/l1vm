// primes-3.l1com - Sieve of Eratosthenes
// primenum search
//
// perf stat on Lenovo Z50-70:
// 2,665914040 seconds time elapsed
// 2,662360000 seconds user
// 0,003336000 seconds sys
//
#include <intr-func.l1h>

(main func)
	(set int64 1 zero 0)
	(set byte 10000000 primes)
	(set int64 1 limit 10000000)
	(set int64 1 one 1)
	(set int64 1 two 2)
	(set int64 1 i 2)
	(set int64 1 j 0)
	(set int64 1 z 1)
	(set int64 1 f 0)
	(set int64 1 a 0)
	(set int64 1 k 0)
	(set int64 1 n 0)
	(set byte 1 oneb 1)
	// array primes is filled by zeroes
	//
	// search primes
	//
	(optimize-if)
	(unsafe)
	(:memory_bounds_off !)
	(two i =)
	(:search_primes)
	((i i *) n =)
	(((n limit <) f =) f if)
		(primes [ i ] a =)
		(((a zero ==) f =) f if)
			(i j =)
			(:search)
			((i j *) k =)
			(((k limit <) f =) f if)
				(oneb primes [ k ] =)
				((j one +) j =)
				(:search jmp)
			(endif)
		(endif)
		((i one +) i =)
		(:search_primes jmp)
	(endif)
	// print primes
	//
	(two i =)
	(:print_primes)
	(((i limit <) f =) f if)
		(primes [ i ] a =)
		(((a zero ==) f =) f if)
            (i :print_i !)
            (:print_n !)
		(endif)
		((i one +) i =)
		(:print_primes jmp)
	(endif)
	(zero :exit !)
    (:memory_bounds_on !)
    (unsafe-end)
(funcend)
