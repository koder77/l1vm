// primes-4.l1com - Sieve of Eratosthenes
// primenum search
//
// perf stat on Lenovo Z50-70: (including printing the numbers)
// 4,474238244 seconds time elapsed
// 2,877650000 seconds user
// 1,358444000 seconds sys
//
(main func)
	(set int64 1 zero 0)
	(set byte 10000000 primes)
	(set int64 1 limit 10000000)
	//(set int64 1 limit 100)
	(set int64 1 one 1)
	(set int64 1 two 2)
	(set int64 1 i 2)
	(set int64 1 j 0)
	(set int64 1 z 1)
	(set int64 1 f 0)
	(set int64 1 a 0)
	(set int64 1 k 0)
	(set int64 1 n 0)
	(set byte 1 oneb 1)
	// array primes is filled by zeroes
	//
	// search primes
	//
	(optimize-if)
	(two i =)
	(:search_primes)
	((i i *) n =)
	(((n limit <) f =) f if)
		(primes [ i ] a =)
		(((a zero ==) f =) f if)
			(i j =)
			(:search)
			((i j *) k =)
			(((k limit <) f =) f if)
				(oneb primes [ k ] =)
				((j one +) j =)
				(:search jmp)
			(endif)
		(endif)
		((i one +) i =)
		(:search_primes jmp)
	(endif)
	// print primes
	//
	(two i =)
	(:print_primes)
	(((i limit <) f =) f if)
		(primes [ i ] a =)
		(((a zero ==) f =) f if)
			(4 i 0 0 intr0)
			(7 0 0 0 intr0)
		(endif)
		((i one +) i =)
		(:print_primes jmp)
	(endif)
	(255 0 0 0 intr0)
(funcend)
