// sdl-math.l1com
// GUI math demo
//
// set a button gadget and get the status
//
#include <intr-func.l1h>
#include <math-const.l1h>
(main func)
	(set const-int64 1 zero 0)
	(set const-int64 1 one 1)
	(set const-int64 1 two 2)
	(set const-int64 1 three 3)
	// screen width and height
	(set const-int64 1 width 500)
	(set const-int64 1 height 100)
	(set const-int64 1 width_b 499)
	(set const-int64 1 height_b 99)
	(set const-int64 1 bit 32)
	(set const-int64 1 gadgets 4)
	// font and background color
	(set const-int64 1 fontsize 20)
	(set string s fontname "fonts/free/FreeMono.ttf")
	(set byte 1 back_r 143)
	(set byte 1 back_g 147)
	(set byte 1 back_b 151)
	(set byte 1 alpha 255)
	(set byte 1 black 0)
	(set int64 1 ret 0)
	(set int64 1 f 0)
	// button gadget text
	(set string s buttonstr "click me!")
	// gadget position
	(set const-int64 1 x 10)
	(set const-int64 1 y 10)
	// set status of button gadget: 1 = gadget selectable, 0 = gadget not active
	(set int64 1 status 1)
	(set string 256 retstr "")
	(set int64 1 value 0)
	(set int64 1 gadget_sel 0)
	(set string s screenopenerrstr "ERROR: can't open screen!")

    // set first number gadget
	(set const-int64 1 x_num_one 10)
	(set const-int64 1 y_num_one 10)
	(set const-string s num_one_str "num1")
	(set const-string 11 num_one_valuestr "")
	(set const-int64 1 num_one_visible 10)
    // set second number gadget
	(set const-int64 1 x_num_two 260)
	(set const-int64 1 y_num_two 10)
	(set const-string s num_two_str "num2")
	(set const-string 11 num_two_valuestr "")
	(set const-int64 1 num_two_visible 10)
    // set add button gadget
	(set const-int64 1 x_add 10)
	(set const-int64 1 y_add 60)
	(set const-string s addstr "add")
	// set quit button gadget
	(set const-int64 1 x_quit 395)
	(set const-int64 1 y_quit 60)
	(set const-string s quitstr "quit")

	(set double 1 num_oned 0.0)
	(set double 1 num_twod 0.0)
	(set double 1 resd 0.0)
	(set string 256 resstr "")
	(set const-int64 1 reslen 255)
	(set const-int64 1 x_res 100)
	(set const-int64 1 y_res 65)
	(set const-int64 1 width_res 394)
	(set const-int64 1 height_res 100)
	(set const-string s zerostr ".0")
	(set const-string s dotstr ".")

	// open screen
	(reset-reg)
    (zero width height bit zero :sdl_open_screen !)
	(ret stpopi)
	// check if screen was opened
	(((ret zero !=) f =) f if)
	    (screenopenerrstr :print_s !)
		(:print_n !)
		(one :exit !)
	(endif)

	(one :math_init !)
	(two :string_init !)

	// load font
    (fontnameaddr fontsize :sdl_font_ttf !)
	// screen is opened, draw background color
	(zero zero width_b height_b back_r back_g back_b alpha :sdl_rectangle_fill !)
	// allocate space for one gadget
	(gadgets :init_gui !)

	// set first number gadget
	(zero x_num_one y_num_one num_one_str num_one_valuestr num_one_visible status :set_gadget_string !)

	// set second number gadget
	(one x_num_two y_num_two num_two_str num_two_valuestr num_two_visible status :set_gadget_string !)

	// set add button gadget
	(two x_add y_add addstr status :set_gadget_button !)

	// set quit button gadget
	(three x_quit y_quit quitstr status :set_gadget_button !)

	(:loop)
	// wait till button gets clicked
	(retstr :gadget_event !)
	// get return values
	(ret stpopi)
	(value stpopi)
	(gadget_sel stpopi)
	(((gadget_sel zero ==) f =) f if)
	    (reset-reg)
	    // get number one
	    (retstr dotstr :string_search !)
	    (ret stpop)
	    (((ret zero <) f =) f if)
	        (retstr zerostr :string_cat !)
	    (endif)
	    (retstr :string_to_double !)
	    (num_oned stpopd)
	(endif)
	(((gadget_sel one ==) f =) f if)
	    (reset-reg)
	    // get number two
	    (retstr dotstr :string_search !)
	    (ret stpop)
	    (((ret zero <) f =) f if)
	        (retstr zerostr :string_cat !)
	    (endif)
	    (retstr :string_to_double !)
	    (num_twod stpopd)
	(endif)
	(((gadget_sel two ==) f =) f if)
	    (reset-reg)
	    (num_oned + num_twod resd :=)

	    (resd :print_d !)
	    (:print_n !)
	    (:print_n !)

	    (resd resstr reslen :string_doubletostring !)
		(x_res y_res width_res height_res back_r back_g back_b alpha :sdl_rectangle_fill !)

        (x_res y_res black black black resstr :sdl_text_ttf !)
	(endif)
	(((gadget_sel three ==) f =) f if)
		// user clicked button, clean up and exit
		(:sdl_free_all_gadgets !)
		(zero :exit !)
	(endif)
	(:loop jmp)
(funcend)
#include <sdl-lib.l1h>
#include <math-lib.l1h>
#include <string.l1h>
